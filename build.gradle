plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'ml.qizd'
version '1'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

sourceCompatibility = "17"
targetCompatibility = "17"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add("--enable-preview")
}

application {
    mainModule = 'ml.qizd.qizdlauncher'
    mainClass = 'ml.qizd.qizdlauncher.MainApplication'
    applicationDefaultJvmArgs += ["--enable-preview"]
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation("com.squareup.okhttp3:okhttp:3.12.1")
    implementation 'org.jetbrains:annotations:24.0.0'
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'qizd-launcher'
    }

    jpackage {
        imageOptions = ["--icon", "src/main/resources/ml/qizd/qizdlauncher/images/icon.ico"]
    }
}

jlinkZip {
    group = 'distribution'
}